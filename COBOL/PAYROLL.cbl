
 IDENTIFICATION DIVISION.
 PROGRAM-ID. PAYROLL.

 ENVIRONMENT DIVISION.
 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
     SELECT EMPFILE ASSIGN TO EMPFILE
         ORGANIZATION IS SEQUENTIAL.
     SELECT RPTFILE ASSIGN TO RPTFILE
         ORGANIZATION IS SEQUENTIAL.

 DATA DIVISION.
 FILE SECTION.
 FD  EMPFILE
     RECORDING MODE IS F
     RECORD CONTAINS 80 CHARACTERS
     BLOCK CONTAINS 0 RECORDS.
 01  EMP-REC.
     COPY EMPREC.
 FD  RPTFILE
     RECORDING MODE IS F
     RECORD CONTAINS 80 CHARACTERS
     BLOCK CONTAINS 0 RECORDS.
 01  RPT-REC                PIC X(80).

 WORKING-STORAGE SECTION.
 01  EOF-SW                 PIC X VALUE 'N'.
     88 EOF                 VALUE 'Y'.
 01  CALC-FIELDS.
     05 BASIC-SALARY-N      PIC 9(7)     COMP-3.
     05 OT-HOURS-N          PIC 9(3)     COMP-3.
     05 TAX-PERC-N          PIC 9(3)     COMP-3.
     05 GROSS-SALARY        PIC 9(9)     COMP-3.
     05 PF-AMT              PIC 9(9)     COMP-3.
     05 TAX-AMT             PIC 9(9)     COMP-3.
     05 NET-PAY             PIC 9(9)     COMP-3.
 01  CONSTS.
     05 PF-PERC             PIC 99  VALUE 12.
     05 OT-RATE             PIC 9(5) VALUE 100.

 01  DISPLAY-LINE.
     05 DL-EMP              PIC X(05).
     05 FILLER              PIC X(01) VALUE ' '.
     05 DL-NAME             PIC X(20).
     05 FILLER              PIC X(02) VALUE '  '.
     05 DL-GROSS            PIC Z(7)9.
     05 FILLER              PIC X(02) VALUE '  '.
     05 DL-PF               PIC Z(6)9.
     05 FILLER              PIC X(02) VALUE '  '.
     05 DL-TAX              PIC Z(6)9.
     05 FILLER              PIC X(02) VALUE '  '.
     05 DL-NET              PIC Z(7)9.

 PROCEDURE DIVISION.
 MAIN-LOGIC.
     PERFORM OPEN-FILES
     PERFORM WRITE-HEADER
     PERFORM UNTIL EOF
        PERFORM READ-EMP
        IF NOT EOF
           PERFORM CALC-PAY
           PERFORM WRITE-DETAIL
        END-IF
     END-PERFORM
     PERFORM CLOSE-FILES
     GOBACK.

 OPEN-FILES.
     OPEN INPUT EMPFILE
          OUTPUT RPTFILE.

 READ-EMP.
     READ EMPFILE
        AT END SET EOF TO TRUE
        NOT AT END
           CONTINUE
     END-READ.

 CALC-PAY.
* Move numeric fields into COMP-3 safe area (strip spaces).
     MOVE BASIC-SALARY TO BASIC-SALARY-N
     MOVE OT-HOURS     TO OT-HOURS-N
     MOVE TAX-PERC     TO TAX-PERC-N

     COMPUTE GROSS-SALARY = BASIC-SALARY-N + (OT-HOURS-N * OT-RATE)
     COMPUTE PF-AMT       = (BASIC-SALARY-N * PF-PERC) / 100
     COMPUTE TAX-AMT      = (GROSS-SALARY * TAX-PERC-N) / 100
     COMPUTE NET-PAY      = GROSS-SALARY - PF-AMT - TAX-AMT.

 WRITE-HEADER.
     MOVE ALL '-' TO RPT-REC
     WRITE RPT-REC
     MOVE 'ID   NAME                 GROSS     PF      TAX     NET'
          TO RPT-REC
     WRITE RPT-REC
     MOVE ALL '-' TO RPT-REC
     WRITE RPT-REC.

 WRITE-DETAIL.
     MOVE EMP-ID    TO DL-EMP
     MOVE EMP-NAME  TO DL-NAME
     MOVE GROSS-SALARY TO DL-GROSS
     MOVE PF-AMT      TO DL-PF
     MOVE TAX-AMT     TO DL-TAX
     MOVE NET-PAY     TO DL-NET
     MOVE DISPLAY-LINE TO RPT-REC
     WRITE RPT-REC.

 CLOSE-FILES.
     CLOSE EMPFILE RPTFILE.
